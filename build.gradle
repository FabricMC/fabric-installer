plugins {
	id 'java'
	id 'maven-publish'
	id "com.github.johnrengelman.shadow" version "7.0.0"
	id "org.cadixdev.licenser" version "0.6.0"
	id "de.undercouch.download" version "4.1.1"
	id "me.modmuss50.remotesign" version "0.1.0"
}

sourceCompatibility = 1.8
version = '0.7.4'
archivesBaseName = "fabric-installer"

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
    mavenCentral()
}

dependencies {
	implementation ('org.sharegov:mjson:1.4.1') {
		transitive false
	}
	testImplementation 'junit:junit:4.13.2'
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

shadowJar {
	manifest {
		attributes 'Implementation-Title': 'FabricInstaller',
				'Implementation-Version': project.version,
				'Main-Class': 'net.fabricmc.installer.Main'
	}

	minimize()
	archiveClassifier.set(null)
	exclude('icon.ico')
}

task serverJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	manifest {
		attributes 'Implementation-Title': 'FabricInstaller',
				'Implementation-Version': project.version,
				'Main-Class': 'net.fabricmc.installer.ServerLauncher'
	}

	minimize()
	exclude('icon.ico')

	archiveClassifier = "server"
	from sourceSets.main.output
	configurations = [project.configurations.compileClasspath]
}
assemble.dependsOn serverJar

def bootstrapVersion = "0.1.3"
def bootstrapArch = "i686"

task downloadBootstrap(type: Download) {
	src "https://maven.fabricmc.net/net/fabricmc/fabric-installer-native-bootstrap/windows-${bootstrapArch}/${bootstrapVersion}/windows-${bootstrapArch}-${bootstrapVersion}.exe"
	dest project.buildDir
}

task nativeExe(dependsOn: [shadowJar, downloadBootstrap], type: FileOutput) {
	output = file("${projectDir}/build/libs/${archivesBaseName}-${project.version}.exe")
	outputs.upToDateWhen { false }

	doFirst {
		output.delete()
	}

	doLast {
		output.createNewFile()
		output.setBytes downloadBootstrap.outputFiles.first().readBytes()

		if (ENV.SIGNING_SERVER) {
			output.append signShadowJar.archiveFile.get().getAsFile().readBytes()
		} else {
			output.append shadowJar.archiveFile.get().getAsFile().readBytes()
		}
	}
}
build.dependsOn nativeExe

jar {
	enabled = false
}

if (ENV.SIGNING_SERVER) {
	remoteSign {
		requestUrl ENV.SIGNING_SERVER
		pgpAuthKey ENV.SIGNING_PGP_KEY
		jarAuthKey ENV.SIGNING_JAR_KEY

		sign (shadowJar)

		afterEvaluate {
			sign publishing.publications.maven
		}
	}

	nativeExe.dependsOn signShadowJar
}

license {
	header rootProject.file("HEADER")
	include "**/*.java"
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version

			if (ENV.SIGNING_SERVER) {
				artifact (signShadowJar) {
					classifier null
				}
			} else {
				artifact (shadowJar) {
					classifier null
				}
			}

			artifact nativeExe.output

			// No point in signing as it is designed to get modified
			artifact (serverJar) {
				classifier "server"
			}
		}
	}
	repositories {
		maven {
			if (ENV.MAVEN_URL) {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def xml = new URL("https://maven.fabricmc.net/net/fabricmc/fabric-installer/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion

class FileOutput extends DefaultTask {
	@OutputFile
	File output
}
