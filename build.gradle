plugins {
	id 'java'
	id 'maven-publish'
	id "checkstyle"
	id "com.github.johnrengelman.shadow" version "7.0.0"
	id "org.cadixdev.licenser" version "0.6.1"
	id "de.undercouch.download" version "5.1.2"
	id "me.modmuss50.remotesign" version "0.1.0"
}

sourceCompatibility = 1.8
version = '0.12.0'
archivesBaseName = "fabric-installer"

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
    mavenCentral()
}

def nativeLibVersion = "0.1.3"
def nativeLibDistributions = [
		"windows-ARM64", "windows-Win32", "windows-x64", "macos-x86_64_arm64"
]

dependencies {
	implementation ('org.sharegov:mjson:1.4.1') {
		transitive false
	}
	nativeLibDistributions.each {
		implementation "net.fabricmc.fabric-installer-native-lib:${it}:${nativeLibVersion}"
	}

	testImplementation 'junit:junit:4.13.2'
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

checkstyle {
	configFile = project.file("checkstyle.xml")
	toolVersion = "8.45"
}

shadowJar {
	manifest {
		attributes 'Implementation-Title': 'FabricInstaller',
				'Implementation-Version': project.version,
				'Main-Class': 'net.fabricmc.installer.Main'
	}

	minimize()
	archiveClassifier.set(null)
	exclude('icon.ico')
}

task serverJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	manifest {
		attributes 'Implementation-Title': 'FabricInstaller',
				'Implementation-Version': project.version,
				'Main-Class': 'net.fabricmc.installer.ServerLauncher'
	}

	minimize()
	exclude('icon.ico')
	exclude('*.png')
	exclude('natives/*')

	archiveClassifier = "server"
	from sourceSets.main.output
	configurations = [project.configurations.compileClasspath]
}
assemble.dependsOn serverJar

def bootstrapVersion = "0.4.0"

task downloadWindowsBootstrap(type: Download) {
	src "https://maven.fabricmc.net/net/fabricmc/fabric-installer-native-bootstrap/windows-Win32/${bootstrapVersion}/windows-Win32-${bootstrapVersion}.exe"
	dest project.buildDir
}

task downloadLinuxBootstrap(type: Download) {
	src "https://maven.fabricmc.net/net/fabricmc/fabric-installer-native-bootstrap/linux-x64/${bootstrapVersion}/linux-x64-${bootstrapVersion}"
	dest project.buildDir
}

abstract class BootstrapTask extends FileOutput {
	@InputFile
	abstract RegularFileProperty getBootstrap()
	@InputFile
	abstract RegularFileProperty getInstallerJar()

	BootstrapTask() {
		installerJar = System.getenv().SIGNING_SERVER ? project.tasks.signShadowJar.archiveFile : project.tasks.shadowJar.archiveFile
		project.tasks.assemble.dependsOn this
	}

	@TaskAction
	def run() {
		def file = output.get().getAsFile()

		file.delete()
		file.createNewFile()
		file.setBytes bootstrap.getAsFile().get().readBytes()
		file.append installerJar.get().getAsFile().readBytes()
	}
}

task windowsBootstrap(dependsOn: [downloadWindowsBootstrap], type: BootstrapTask) {
	bootstrap = downloadWindowsBootstrap.outputFiles.first()
	output = file("${projectDir}/build/libs/${archivesBaseName}-${project.version}.exe")
}

task linuxBootstrap(dependsOn: [downloadLinuxBootstrap], type: BootstrapTask) {
	bootstrap = downloadLinuxBootstrap.outputFiles.first()
	output = file("${projectDir}/build/libs/${archivesBaseName}-${project.version}-linux")

	doLast {
		// Set executable. Will likely get lost, but worth a try.
		output.get().getAsFile().setExecutable(true, false);
	}
}

jar {
	enabled = false
}

if (ENV.SIGNING_SERVER) {
	remoteSign {
		requestUrl ENV.SIGNING_SERVER
		pgpAuthKey ENV.SIGNING_PGP_KEY
		jarAuthKey ENV.SIGNING_JAR_KEY

		sign (shadowJar)

		afterEvaluate {
			sign publishing.publications.maven
		}
	}
}

license {
	header rootProject.file("HEADER")
	include "**/*.java"
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version

			if (ENV.SIGNING_SERVER) {
				artifact (signShadowJar) {
					classifier null
				}
			} else {
				artifact (shadowJar) {
					classifier null
				}
			}

			artifact windowsBootstrap.output
			artifact (linuxBootstrap.output) {
				classifier "linux"
				extension null
			}

			// No point in signing as it is designed to get modified
			artifact (serverJar) {
				classifier "server"
			}
		}
	}
	repositories {
		maven {
			if (ENV.MAVEN_URL) {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def xml = new URL("https://maven.fabricmc.net/net/fabricmc/fabric-installer/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion

abstract class FileOutput extends DefaultTask {
	@OutputFile
	abstract RegularFileProperty getOutput()
}
